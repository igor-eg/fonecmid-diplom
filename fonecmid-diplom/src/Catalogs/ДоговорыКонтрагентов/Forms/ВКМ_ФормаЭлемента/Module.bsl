#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВКМ_ДобавитьСозданныеРеквизиты();
	ВКМ_ИзменитьВидимостьГруппыПараметрыОбслуживанияНаСервере();	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	
	ВКМ_ИзменитьВидимостьГруппыПараметрыОбслуживания();
	
КонецПроцедуры
#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВКМ_ДобавитьСозданныеРеквизиты()

	ВКМ_ГруппаПараметрыОбслуживания = Элементы.Добавить("ВКМ_ГруппаПараметрыОбслуживания", Тип("ГруппаФормы"), ЭтотОбъект);
	ВКМ_ГруппаПараметрыОбслуживания.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ВКМ_ГруппаПараметрыОбслуживания.ОтображатьЗаголовок = Истина;
	ВКМ_ГруппаПараметрыОбслуживания.Заголовок = "Данные договора абонентского обслуживания";
	ВКМ_ГруппаПараметрыОбслуживания.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ВКМ_ГруппаПараметрыОбслуживания.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ВКМ_ГруппаПараметрыОбслуживания.ОтображатьОтступСлева = Истина;
	ВКМ_ГруппаПараметрыОбслуживания.Отображение = ОтображениеОбычнойГруппы.Нет;
	ВКМ_ГруппаПараметрыОбслуживания.ОтображениеУправления = ОтображениеУправленияОбычнойГруппы.Картинка;
	
	ВКМ_ДекорацияДатаОбслуживания = Элементы.Добавить("ВКМ_ДекорацияПериодОбслуживания", Тип("ДекорацияФормы"), ВКМ_ГруппаПараметрыОбслуживания);
	ВКМ_ДекорацияДатаОбслуживания.Вид = ВидДекорацииФормы.Надпись;
	ВКМ_ДекорацияДатаОбслуживания.Заголовок = "Период действия договора:";
	
	ВКМ_ГруппаДатаОбслуживания = Элементы.Добавить("ВКМ_ГруппаДатаОбслуживания", Тип("ГруппаФормы"), ВКМ_ГруппаПараметрыОбслуживания);
	ВКМ_ГруппаДатаОбслуживания.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ВКМ_ГруппаДатаОбслуживания.ОтображатьЗаголовок = Ложь;
	ВКМ_ГруппаДатаОбслуживания.Заголовок = "Период действия договора";
	ВКМ_ГруппаДатаОбслуживания.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ВКМ_ГруппаДатаОбслуживания.ОтображатьОтступСлева = Истина;
		
	ВКМ_ДатаНачалаДействияДоговора = Элементы.Добавить("ВКМ_ДатаНачалаДействияДоговора", Тип("ПолеФормы"), ВКМ_ГруппаДатаОбслуживания);
	ВКМ_ДатаНачалаДействияДоговора.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_ДатаНачалаДействияДоговора.ПутьКДанным = "Объект.ВКМ_ДатаНачалаДействияДоговора";
	
	ВКМ_ДатаОкончанияДействияДоговора = Элементы.Добавить("ВКМ_ДатаОкончанияДействияДоговора", Тип("ПолеФормы"), ВКМ_ГруппаДатаОбслуживания);
	ВКМ_ДатаОкончанияДействияДоговора.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_ДатаОкончанияДействияДоговора.ПутьКДанным = "Объект.ВКМ_ДатаОкончанияДействияДоговора";
	
	ВКМ_СуммаАбонентскойПлаты = Элементы.Добавить("ВКМ_СуммаАбонентскойПлаты", Тип("ПолеФормы"), ВКМ_ГруппаПараметрыОбслуживания);
	ВКМ_СуммаАбонентскойПлаты.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_СуммаАбонентскойПлаты.ПутьКДанным = "Объект.ВКМ_СуммаАбонентскойПлаты";

	ВКМ_СтоимостьЧасаРаботы = Элементы.Добавить("ВКМ_СтоимостьЧасаРаботы", Тип("ПолеФормы"), ВКМ_ГруппаПараметрыОбслуживания);
	ВКМ_СтоимостьЧасаРаботы.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_СтоимостьЧасаРаботы.ПутьКДанным = "Объект.ВКМ_СтоимостьЧасаРаботы";
	
КонецПроцедуры

&НаСервере
Процедура ВКМ_ИзменитьВидимостьГруппыПараметрыОбслуживанияНаСервере()
	Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		Элементы.ВКМ_ГруппаПараметрыОбслуживания.Видимость = Ложь;
	Иначе
		Элементы.ВКМ_ГруппаПараметрыОбслуживания.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВКМ_ИзменитьВидимостьГруппыПараметрыОбслуживания()
	Если Объект.ВидДоговора <> ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание") Тогда
		Элементы.ВКМ_ГруппаПараметрыОбслуживания.Видимость = Ложь;
	Иначе
		Элементы.ВКМ_ГруппаПараметрыОбслуживания.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
